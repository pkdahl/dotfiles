# .zshrc is sourced in interactive shells.

# Emacs keybindings
bindkey -e

autoload -U compinit
compinit -d ${HOME}/.zsh/zcompdump

# Any directory changing command is treated as pushd
setopt AUTO_PUSHD
DIRSTACKSIZE=100
# allow tab completion in the middle of a word
setopt COMPLETE_IN_WORD

# Never beep
setopt NO_BEEP

setopt APPEND_HISTORY     # Allows appending of new history to HISTFILE instead of overwrite
setopt INC_APPEND_HISTORY # Commands are added to history as they are executed
setopt SHARE_HISTORY
setopt HIST_VERIFY        # Allow editing of command instead of running it immediately
setopt HIST_FIND_NO_DUPS  # Backward search with editor do not show commands more than once
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_EXPIRE_DUPS_FIRST
HISTFILE=${HOME}/.zsh/zhistory
HISTSIZE=10240
SAVEHIST=$HISTSIZE


#-- Aliases --------------------------------------------------------------------

alias h='history -fi'
alias d='dirs -v'
alias o='popd'
alias p='pushd'

# no spelling correction
alias mv='nocorrect mv -i'
alias cp='nocorrect cp -i'
alias rm='nocorrect rm -i'
alias mkdir='nocorrect mkdir'

# ls
alias ls='ls -G'           # always colours
alias ll='ls -l'
alias l.='ls -A'
alias ll.='ls -Al'
alias lsd='ls -ld *(-/DN)' # list only dirs

# misc
alias df='df -h'
alias du='du -h'


#-- Prompt ---------------------------------------------------------------------

# http://stevelosh.com/blog/2010/02/my-extravagant-zsh-prompt/
# https://github.com/robbyrussell/oh-my-zsh

autoload -U colors && colors

setopt promptsubst

function git_prompt_info() {
  ref=$(git symbolic-ref HEAD 2> /dev/null) || return
  echo "$ZSH_THEME_GIT_PROMPT_PREFIX$(parse_git_dirty)${ref#refs/heads/}$ZSH_THEME_GIT_PROMPT_SUFFIX"
}

parse_git_dirty() {
  local STATUS=''
  local FLAGS
  FLAGS=('--porcelain')
  STATUS=$(command git status ${FLAGS} 2> /dev/null | tail -n1)
  if [[ -n $STATUS ]]; then
    echo "$ZSH_THEME_GIT_PROMPT_DIRTY"
  else
    echo "$ZSH_THEME_GIT_PROMPT_CLEAN"
  fi
}

ZSH_THEME_GIT_PROMPT_PREFIX="on "
ZSH_THEME_GIT_PROMPT_SUFFIX="%{$reset_color%}"
ZSH_THEME_GIT_PROMPT_DIRTY="%{$fg[red]%}"
ZSH_THEME_GIT_PROMPT_CLEAN="%{$fg[green]%}"

function prompt_char() {
    git branch >/dev/null 2>/dev/null && echo '±' && return
    echo '○'
}

PROMPT='
%{$fg[cyan]%}%n%{$reset_color%} at %{$fg[magenta]%}%m%{$reset_color%} in %{$fg[blue]%}%~%{$reset_color%} $(git_prompt_info)
$(prompt_char) '
